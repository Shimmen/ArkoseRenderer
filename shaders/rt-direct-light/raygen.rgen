#version 460

#include <common/namedUniforms.glsl>
#include <rt-direct-light/common.glsl>
#include <common/spherical.glsl>
#include <common/rayTracing.glsl>
#include <shared/CameraState.h>

layout(set = 0, binding = 0) uniform AccelerationStructure topLevelAS;
layout(set = 0, binding = 1) uniform CameraStateBlock { CameraState camera; };
layout(set = 0, binding = 2) uniform sampler2D environmentMap;
layout(set = 0, binding = 3, rgba16f) uniform image2D u_image;

NAMED_UNIFORMS_STRUCT(PushConstants, pushConstants)

layout(location = 0) rayPayload RayPayload payload;

void main()
{
	const vec2 pixelCenter = vec2(rt_LaunchID.xy) + vec2(0.5);
	const vec2 inUV = pixelCenter / vec2(rt_LaunchSize.xy);
	vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = camera.worldFromView * vec4(0, 0, 0, 1);
	vec4 target = camera.viewFromProjection * vec4(d.x, d.y, 1.0, 1.0);
	vec4 direction = camera.worldFromView * vec4(normalize(target.xyz / target.w), 0.0);

	float tmin = camera.zNear;
	float tmax = camera.zFar;

	int numHits = 0;
	vec3 color = vec3(0.0);

	// Opaque
	{
		uint rayFlags = RayFlags_Opaque | RayFlags_CullBackFacingTriangles;
		uint cullMask = RT_HIT_MASK_OPAQUE;

		traceRay(topLevelAS, rayFlags, cullMask, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);
		if (payload.hitT != HIT_T_MISS) {
			tmax = payload.hitT;
			color = payload.color;
			numHits += 1;
		}
	}

	// Masked
	{
		uint rayFlags = RayFlags_NoOpaque;
		uint cullMask = RT_HIT_MASK_MASKED;

		traceRay(topLevelAS, rayFlags, cullMask, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);
		if (payload.hitT != HIT_T_MISS) {
			tmax = payload.hitT;
			color = payload.color;
			numHits += 1;
		}
	}

	// Blended
	{
		// TODO: Maybe just use stochastic transparency and count this as masked?
	}

	// Draw environment if miss
	if (numHits == 0) {
		vec2 sampleUv = sphericalUvFromDirection(direction.xyz);
		color = pushConstants.environmentMultiplier * texture(environmentMap, sampleUv).rgb;
	}

	imageStore(u_image, ivec2(rt_LaunchID.xy), vec4(color, 0.0));
}
