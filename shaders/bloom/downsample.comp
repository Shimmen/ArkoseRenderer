#version 460

#include <common.glsl>
#include <common/sampling.glsl>

layout(set = 0, binding = 0, rgba16f) restrict writeonly uniform image2D targetImg;
layout(set = 0, binding = 1, rgba16f) restrict readonly  uniform image2D sourceImg;

layout(push_constant) uniform PushConstants {
    bool applyNormalization;
};

vec3 sampleSourceBilinear(vec2 uv)
{
    ivec2 sourceMax = imageSize(sourceImg) - ivec2(1, 1);

    vec2 clampedUv = clamp(uv, vec2(0.0), vec2(1.0));
    vec2 coord = clampedUv * vec2(sourceMax);

    ivec2 iCoord = ivec2(coord);
    vec2 fCoord = fract(coord);

    vec4 tl = imageLoad(sourceImg, min(iCoord + ivec2(0, 0), sourceMax));
    vec4 tr = imageLoad(sourceImg, min(iCoord + ivec2(1, 0), sourceMax));
    vec4 bl = imageLoad(sourceImg, min(iCoord + ivec2(0, 1), sourceMax));
    vec4 br = imageLoad(sourceImg, min(iCoord + ivec2(1, 1), sourceMax));
    vec4 filtered = bilinearFilter(tl, tr, bl, br, fCoord);

    return filtered.rgb;
}

// See https://www.iryoku.com/next-generation-post-processing-in-call-of-duty-advanced-warfare
vec4 codCustomDownsample(vec2 uv, vec2 targetTexelSize)
{
    vec2 off = targetTexelSize;
    vec2 halfOff = off / 2.0;

    vec3 sampleGroups[4] = vec3[](vec3(0.0), vec3(0.0), vec3(0.0), vec3(0.0));

    // Center 4x4 box, weight 0.5
    sampleGroups[0] += (0.5 / 4.0) * sampleSourceBilinear(uv + vec2(-halfOff.x, -halfOff.y));
    sampleGroups[0] += (0.5 / 4.0) * sampleSourceBilinear(uv + vec2(-halfOff.x, +halfOff.y));
    sampleGroups[0] += (0.5 / 4.0) * sampleSourceBilinear(uv + vec2(+halfOff.x, -halfOff.y));
    sampleGroups[0] += (0.5 / 4.0) * sampleSourceBilinear(uv + vec2(+halfOff.x, +halfOff.y));

    // Top-left & top-right & bottom-left & bottom right samples (not shared), weight 0.125
    sampleGroups[1] += (0.125 / 4.0) * sampleSourceBilinear(uv + vec2(-off.x, -off.y));
    sampleGroups[1] += (0.125 / 4.0) * sampleSourceBilinear(uv + vec2(-off.x, +off.y));
    sampleGroups[1] += (0.125 / 4.0) * sampleSourceBilinear(uv + vec2(+off.x, -off.y));
    sampleGroups[1] += (0.125 / 4.0) * sampleSourceBilinear(uv + vec2(+off.x, +off.y));

    // Centered "plus sign", where every sample is shared by two 4x4 boxes, weight 0.125
    sampleGroups[2] += (0.25 / 4.0) * sampleSourceBilinear(uv + vec2(-off.x, 0.0));
    sampleGroups[2] += (0.25 / 4.0) * sampleSourceBilinear(uv + vec2(+off.x, 0.0));
    sampleGroups[2] += (0.25 / 4.0) * sampleSourceBilinear(uv + vec2(0.0, -off.y));
    sampleGroups[2] += (0.25 / 4.0) * sampleSourceBilinear(uv + vec2(0.0, +off.y));

    // Center sample, shared by all four offset 4x4 boxes, weight 0.125
    // (divide by 4 to get each sample in the 4x4, but then multiply with 4 since the overlap)
    sampleGroups[3] += 0.125 * sampleSourceBilinear(uv);

    if (applyNormalization) {
        vec4 sum = vec4(0.0);
        for (uint i = 0; i < 4; ++i) {
            vec3 sampleGroup = sampleGroups[i];
            float weight = 1.0 / (1.0 + luminance(sampleGroup));
            sum += weight * vec4(sampleGroup, 1.0);
        }
        // x4 since it's not a weighted average, but a weighted sum in the way we're doing it here
        return vec4(4.0 * sum.xyz / sum.w, 1.0);
    } else {
        return vec4(sampleGroups[0] + sampleGroups[1] + sampleGroups[2] + sampleGroups[3], 1.0);
    }
}

layout(local_size_x = 16, local_size_y = 16) in;
void main()
{
    ivec2 targetSize = imageSize(targetImg);

    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(pixelCoord, targetSize)))
        return;

    vec2 targetTexelSize = 1.0 / vec2(targetSize);
    vec2 uv = (vec2(pixelCoord) + 0.5) * targetTexelSize;
    vec4 color = codCustomDownsample(uv, targetTexelSize);

    imageStore(targetImg, pixelCoord, color);
}
