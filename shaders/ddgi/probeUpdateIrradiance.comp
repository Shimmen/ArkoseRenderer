#version 460

#include <common.glsl>
#include <common/namedUniforms.glsl>
#include <common/octahedral.glsl>
#include <ddgi/common.glsl>

layout(set = 0, binding = 0, rgba16f) uniform readonly image2D surfelImage;
layout(set = 0, binding = 1, rgba16f) uniform          image2D probeIrradianceAtlas;

NAMED_UNIFORMS(pushConstants,
    ivec3 gridDimensions;
    uint raysPerProbe;
    uint firstProbeIdx;
    float hysterisis;
    uint frameIdx;
)

layout(local_size_x = DDGI_IRRADIANCE_RES,
       local_size_y = DDGI_IRRADIANCE_RES,
       local_size_z = 1) in;
void main()
{
    uint surfelProbeIdx = gl_GlobalInvocationID.z;
    const uint probeCount = pushConstants.gridDimensions.x * pushConstants.gridDimensions.y * pushConstants.gridDimensions.z;
    uint probeIdx = (surfelProbeIdx + pushConstants.firstProbeIdx) % probeCount;
    ivec2 tileTexelCoord = ivec2(gl_GlobalInvocationID.xy);

    // Compute the direction that this ocrahedral texel (coordinate) represents
    vec2 tileUV = (vec2(tileTexelCoord) + vec2(0.5)) / vec2(DDGI_IRRADIANCE_RES);
    vec3 texelDirection = octahedralDecode(2.0 * tileUV - 1.0);

    // Compute the texel coordinate that we should draw into
    const int tileResolution = DDGI_IRRADIANCE_RES;
    const int tilePadding = DDGI_ATLAS_PADDING;
    ivec2 atlasTexelCoord = calculateAtlasTexelCoord(probeIdx, pushConstants.gridDimensions, tileTexelCoord, tileResolution, tilePadding);

    vec3 newIrradiance = vec3(0.0);
    float totalWeight = 0.0;

    for (uint sampleIdx = 0; sampleIdx < pushConstants.raysPerProbe; ++sampleIdx) {

        vec3 rayDirection = calculateRotatedSphericalFibonacciSample(probeIdx, sampleIdx, pushConstants.raysPerProbe, pushConstants.frameIdx);
        float weight = max(0.0, dot(texelDirection, rayDirection));

        vec3 surfelRadiance = imageLoad(surfelImage, ivec2(surfelProbeIdx, sampleIdx)).rgb;

        newIrradiance += weight * surfelRadiance;
        totalWeight += weight;
    }

    const float epsilon = 1e-9 * float(pushConstants.raysPerProbe);
    newIrradiance /= max(totalWeight, epsilon);

    // From example code: "Store gamma encoded for more perceptually linear EMWA"
    // NOTE: Ensure we do the equivalent gamma decoding while sampling probes.
    newIrradiance = pow(newIrradiance, vec3(DDGI_IRRADIANCE_INV_GAMMA));

    vec3 oldIrradiance = imageLoad(probeIrradianceAtlas, atlasTexelCoord).rgb;
    float changeMagnitude = abs(maxComponent(newIrradiance - oldIrradiance));

    float hysteresis = pushConstants.hysterisis;
    
    // From example code: "Lower the hysteresis when a large change is detected"
    //const float significantChangeThreshold = 0.25;
    //if (changeMagnitude > significantChangeThreshold) {
    //    hysteresis = max(0.0, hysteresis - 0.15);
    //}

    // From example code: if there is signficant change, assume new distribution
    //const float newDistributionChangeThreshold = 0.8;
    //if (changeMagnitude > newDistributionChangeThreshold) {
    //    hysteresis = 0.0;
    //}

    // "Hysteresis represent the history dependence of physical systems", hence the mix from new to old with hysteresis
    newIrradiance = mix(newIrradiance, oldIrradiance, hysteresis);
    imageStore(probeIrradianceAtlas, atlasTexelCoord, vec4(newIrradiance, 0.0));
}
