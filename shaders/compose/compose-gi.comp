#version 460

#include <common/namedUniforms.glsl>

layout(set = 0, binding = 0, rgba16f) uniform writeonly image2D sceneColorWithGI;
layout(set = 0, binding = 1) uniform sampler2D sceneColorBeforeGI;
layout(set = 0, binding = 2) uniform sampler2D baseColorTex;
layout(set = 0, binding = 3) uniform sampler2D ambientOcclusionTex;
layout(set = 0, binding = 4) uniform sampler2D diffuseGiTex;

NAMED_UNIFORMS(pushConstants,
    uvec2 targetSize;
    bool includeDiffuseGI;
    bool withMaterialColor;
    bool withAmbientOcclusion;
)

layout(local_size_x = 32, local_size_y = 32) in;
void main()
{
    ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(pixelCoord, pushConstants.targetSize)))
        return;

    vec4 sceneColor = texelFetch(sceneColorBeforeGI, pixelCoord, 0);

    float ambientOcclusion = 1.0;
    if (pushConstants.withAmbientOcclusion) {
        ambientOcclusion = texelFetch(ambientOcclusionTex, pixelCoord, 0).r;
    }

    vec3 materialBaseColor = vec3(1.0);
    if (pushConstants.withMaterialColor) {
        materialBaseColor = texelFetch(baseColorTex, pixelCoord, 0).rgb;
    }

    if (pushConstants.includeDiffuseGI) {
        vec3 diffuseGI = texelFetch(diffuseGiTex, pixelCoord, 0).rgb;
        sceneColor.rgb = diffuseGI * materialBaseColor * ambientOcclusion;
    }

    imageStore(sceneColorWithGI, pixelCoord, sceneColor);
}
